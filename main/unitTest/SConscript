#Remember the macports command:  port contents packageName
			
# Import and clone the environement
Import('testEnv')
localEnv=testEnv.Clone()

# Get from the env the list of all objects used to 
# make the VPP
allObjects= localEnv['objList']

#Compile Debug mode
#env.Append(CCFLAGS= '-g')
#env.Append(CPPFLAGS ='-Wc++11-extensions')

# Warning: use of enumeration in a nested name specifier is a
#      C++11 extension [-Wc++11-extensions]

# ------- Add the third_parties -----------------

# Define a dictionary that will store the thirdParties
thirdPartyDict={}

# Import the third_parties required for the tests.
import thirdParties
thirdPartyDict['System']= thirdParties.System(localEnv)
thirdPartyDict['CppUnit']= thirdParties.CppUnit(localEnv)
thirdPartyDict['Eigen']= thirdParties.Eigen(localEnv)
thirdPartyDict['Qt']= thirdParties.Qt(localEnv)
thirdPartyDict['Boost']= thirdParties.Boost(localEnv)
thirdPartyDict['NLOpt']= thirdParties.NLOpt(localEnv)
thirdPartyDict['IPOpt']= thirdParties.IPOpt(localEnv)

# ----------------------------------------------

autotestsProgram= localEnv.Program('unitTest', Glob('*.cpp') +
							allObjects
							)

# Add the path of the program to the env
localEnv.Append( TEST_EXE_PATH=autotestsProgram[0].abspath )
localEnv.Append( THIRDPARTYDICT=thirdPartyDict )

# As the exe has been installed, modify it to use the local Qt frameworks
fixDynamicLibPath_commandTest = Command('fixDynamicLibPathTest', [], localEnv.fixDynamicLibPathTest)
Depends( fixDynamicLibPath_commandTest, autotestsProgram )
Default( fixDynamicLibPath_commandTest )

# Add a target 'runTest' that will ensure that it's built before running the test suite
test_alias = Alias('runTest', [autotestsProgram], autotestsProgram[0].path)
Depends( test_alias, fixDynamicLibPath_commandTest) 

# Simply required.  Without it, 'test' is never considered out of date.
AlwaysBuild(test_alias)
